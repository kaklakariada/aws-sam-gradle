plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'com.github.hierynomus.license' version '0.14.0'
}

repositories {
   jcenter()
}

version = '0.4.0'
group = 'com.github.kaklakariada.aws'

ext {
  awsApiVersion = '1.11.163'
  gradlePluginId = 'com.github.kaklakariada.aws-sam-deploy'
}

dependencies {
    compile "com.amazonaws:aws-java-sdk-s3:$awsApiVersion"
    compile "com.amazonaws:aws-java-sdk-cloudformation:$awsApiVersion"
    compile "com.amazonaws:aws-java-sdk-sts:$awsApiVersion"
    
    testCompile 'junit:junit:4.12'
}

gradlePlugin {
    plugins {
        awsSamDeployPlugin {
            id = gradlePluginId
            implementationClass = 'com.github.kaklakariada.aws.sam.AwsSamDeployPlugin'
        }
    }
}

pluginBundle {
  website = 'https://github.com/kaklakariada/aws-sam-gradle/blob/master/README.md'
  vcsUrl = 'https://github.com/kaklakariada/aws-sam-gradle'
  description = 'Gradle plugin for deploying AWS Serverless Application Models using CloudFormation'
  tags = ['aws', 'serverless', 'serverless application model', 'sam',
          'cloudformation', 'deployment', 'api gateway', 'lambda']

  plugins {
    awsSamDeployPlugin {
      id = gradlePluginId
      displayName = 'AWS serverless deployment plugin'
    }
  }
}

test {
    testLogging {
        showStandardStreams = true
    }
}

test.onlyIf { rootProject.name == 'aws-sam-gradle' }

license {
    header = file('gradle/license-header.txt')
    strictCheck = true
    ext.year = 2017
    ext.name = 'Christoph Pirkl'
    ext.email = 'christoph at users.sourceforge.net'
}
